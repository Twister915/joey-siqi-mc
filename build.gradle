plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.0.0-beta4'
}

group = 'sh.joey'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = 'papermc'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT'
    implementation 'io.reactivex.rxjava3:rxjava:3.1.12'
    implementation 'com.zaxxer:HikariCP:5.1.0'
    implementation 'org.postgresql:postgresql:42.7.4'
}

tasks.named('build') {
    dependsOn shadowJar
}

shadowJar {
    archiveClassifier.set('')
    relocate 'io.reactivex.rxjava3', 'sh.joey.mc.shaded.rxjava3'
    relocate 'org.reactivestreams', 'sh.joey.mc.shaded.reactivestreams'
    relocate 'com.zaxxer.hikari', 'sh.joey.mc.shaded.hikari'
    // PostgreSQL driver uses ServiceLoader - don't relocate
}

test {
    useJUnitPlatform()
}
